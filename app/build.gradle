plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.dagger.hilt
    alias libs.plugins.google.services
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "com.jean.cinemappcompose"
        minSdk 24
        targetSdk 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        buildConfigField "String", "TMDB_API_KEY", properties.getProperty("TMDB_API_KEY")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {

    implementation libs.androidx.ktx
    implementation libs.compose.ui
    implementation libs.compose.material
    implementation libs.compose.material3
    implementation libs.compose.ui.tooling.preview
    implementation libs.androidx.compose.activity
    implementation libs.compose.material.icons
    implementation libs.androidx.lifecycle.ktx

    // Coroutines
    implementation libs.jetbrains.kotlinx.coroutines
    implementation libs.jetbrains.kotlinx.coroutines.play.services

    // Navigation
    implementation libs.androidx.navigation.compose

    // Retrofit
    implementation libs.squareup.retrofit
    implementation libs.squareup.retrofit.converter

    // Dagger Hilt
    implementation libs.dagger.hilt
    implementation libs.dagger.hilt.navigation.compose
    kapt libs.dagger.hilt.compiler

    // Room Database
    implementation libs.room.runtime
    implementation libs.room.ktx
    kapt libs.room.compiler

    // Firebase
    implementation libs.firebase.auth
    implementation platform(libs.firebase.bom)
    implementation libs.firebase.auth.ktx
    implementation libs.firebase.firestore.ktx

    // Coil
    implementation libs.coil.compose

    // Status bar customization
    implementation libs.accompanist.system.ui

    // Splash Screen
    implementation libs.splash.screen

    // Testing
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso
    androidTestImplementation libs.compose.test.junit
    debugImplementation libs.compose.test.ui.tooling
    debugImplementation libs.compose.test.manifest

}